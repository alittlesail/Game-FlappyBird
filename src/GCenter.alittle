
//[[
//	编辑器中心逻辑，主要处理主界面逻辑
//]]

namespace FlappyBird;

protected var g_GConfig:ALittle.IJsonConfig;

class GCenter
{
    // 层
    private ALittle.DisplayLayout _main_layer;
    private ALittle.DisplayLayout _dialog_layer;

    // 主菜单界面
    private ALittle.DisplayLayout _main_menu;
    private ALittle.DisplayObject _game_over;
    private ALittle.DisplayObject _game_title;
    private ALittle.ImagePlay _bird_image;
    private ALittle.LoopList _bird_image_loop;

    // 游戏界面
    private ALittle.DisplayObject _main_scene;
    
    // 新手提示
    private ALittle.DisplayObject _tip_1;
    private ALittle.DisplayObject _tip_2;

    // 分数
    private ALittle.Text _max_score_text;
    private ALittle.Text _score_text;
    
    // 水管
    private ALittle.DisplayLayout _pipe_container;
    private int _next_pipe_gap;
    private int _next_pipe_up;

    // 鸟
    private ALittle.ImagePlay _bird;
    private ALittle.LoopFrame _frame_anti;

    // 地板
    private int _ground_frame_acc;
    private ALittle.DisplayObject _ground;
    private ALittle.DisplayObject _bg;

    private double _fly_a = 20;
    private double _fly_power = 5;
    private bool _dieing = false;
    private double _fly_y_rate = 0;

    [Language(Lua)]
    private deeplearning.DeeplearningDqnDnnModel _dqn_model;
    private string _dqn_model_path;

    private double _dqn_range = 0;
    private ALittle.DisplayObject _reward_text;
    private ALittle.DisplayObject _target_x_text;
    private ALittle.DisplayObject _target_y_text;

    public ctor()
    {
	}
    
    public fun Setup()
    {
        // 获取配置信息
        g_GConfig = ALittle.CreateConfigSystem(g_ModuleBasePath.."/User.cfg");
        // 生成随机数种子
        ALittle.Math_RandomSeed(ALittle.Time_GetCurTime());
        // 设置5个线程
        ALittle.System_SetThreadCount(1);
    
        // 创建主层
        this._main_layer = new ALittle.DisplayLayout(g_Control);
        this._main_layer.width_type = ALittle.UIEnumTypes.SIZE_MARGIN;
        this._main_layer.height_type = ALittle.UIEnumTypes.SIZE_MARGIN;
        g_LayerGroup.AddChild(this._main_layer, null);
        this._dialog_layer = new ALittle.DisplayLayout(g_Control);
        this._dialog_layer.width_type = ALittle.UIEnumTypes.SIZE_MARGIN;
        this._dialog_layer.height_type = ALittle.UIEnumTypes.SIZE_MARGIN;
        g_LayerGroup.AddChild(this._dialog_layer, null);
    
        // 创建游戏界面
        this._main_scene = g_Control.CreateControl{ALittle.DisplayObject}("main_scene", this, this._main_layer);
        this._main_scene.visible = false;

        // 创建主菜单界面
        this._main_menu = g_Control.CreateControl{ALittle.DisplayLayout}("main_menu", this, null);
        this._dialog_layer.AddChild(this._main_menu, null);
        this._main_menu.visible = true;

        // 创建主菜单界面鸟的动画
        {
            var y_value = this._bird_image.y_value;
            var min_y_value = y_value - 10;
            var max_y_value = y_value + 10;

            this._bird_image_loop = new ALittle.LoopList();
            this._bird_image_loop.AddUpdater(new ALittle.LoopLinear(this._bird_image, "y_value", min_y_value, 1000, 0));
            var linear_list = new ALittle.LoopList();
            linear_list.AddUpdater(new ALittle.LoopLinear(this._bird_image, "y_value", max_y_value, 2000, 0));
            linear_list.AddUpdater(new ALittle.LoopLinear(this._bird_image, "y_value", min_y_value, 2000, 0));
            linear_list._user_data = linear_list;
            this._bird_image_loop.AddUpdater(new ALittle.LoopRepeat(linear_list, -1));

            this._bird_image_loop.Start();
        }

        this._game_over.visible = false;
        this._game_title.visible = true;

        // 游戏界面分数计算
        this._max_score_text._user_data = g_GConfig.GetConfig("max_score", 0);        
        this._max_score_text.text = this._max_score_text._user_data;
        
        // 帧动画
        this._frame_anti = new ALittle.LoopFrame(bind(this.LoopGroundFrame, this));

        if (deeplearning != null && deeplearning.DeeplearningDqnDnnModel != null)
        {
            var state_num = 3;
            var action_num = 2;
            this._dqn_model = new deeplearning.DeeplearningDqnDnnModel(state_num, action_num, 100, 2000);

            this._dqn_model_path = g_ModuleBasePath.."/Other/flappybird_"..state_num.."_"..action_num..".model";
            this._dqn_model.Load(this._dqn_model_path);

            this._tip_1.visible = false;
            this._tip_2.visible = false;
        }

        this._reward_text.visible = this._dqn_model != null;
        this._target_x_text.visible = this._dqn_model != null;
        this._target_y_text.visible = this._dqn_model != null;
    }

    public fun Restart()
    {
        this._ground_frame_acc = 0;
        this._next_pipe_gap = 0;
        this._next_pipe_up = 50;
        this._bird.y = this._ground.y / 2;
        this._bird.x = this._bird.width;
        this._bird.angle = 0;
        this._bird.visible = true;
        this._bird.Play();
        this._game_over.visible = false;
        this._dieing = false;
        this._fly_y_rate = 0;
        
        // 分数清0
        this._score_text.text = "0";
        this._score_text._user_data = 0;

        // 清空容器
        this._pipe_container.RemoveAllChild();

        this._frame_anti.Start();
    }

    private fun CalcState() : List<double>
    {
        var dst_x, start_y, end_y, total_y, center_y = this.CalcRange();

        var state = new List<double>();
        state[1] = center_y;
        state[2] = this._bird.y;
        state[3] = this._fly_y_rate;
    
        return state;
    }

    private fun CalcRange() : double, double, double, double, double
    {
        var dst_x:double = A_UISystem.view_width;
        var start_y:double = this._ground.y / 4;
        var end_y:double = this._ground.y / 4 * 3;

        for (var i, child in this._pipe_container.childs)
        {
            if (child.x + child.width >= this._bird.x - this._bird.width / 2)
            {
                dst_x = child.x + child.width + this._bird.width / 2;
                // 上面的水管
                if (child._user_data == true)
                {
                    start_y = child.y + child.height + this._bird.height / 2 + this._dqn_range;
                    end_y = this._ground.y;
                }
                else
                {
                    start_y = 0;
                    end_y = child.y - this._bird.height / 2 - this._dqn_range;
                }

                break;
            }
        }

        var total_y = end_y - start_y;
        var center_y = total_y / 2 + start_y;

        return dst_x, start_y, end_y, total_y, center_y;
    }

    private fun CalcReward(bool die) : double
    {
        // if (die) return -100;

        // 当前位置
        var src_x = this._bird.x;
        var src_y = this._bird.y;

        var dst_x, start_y, end_y, total_y, center_y = this.CalcRange();

        var reward = 0.0;

        // 距离边缘越近，速度越快，分数越低
        {
            var max = 100;

            if (src_y > center_y)
            {
                var k = max / (center_y - end_y);
                var b = max;

                reward += k * (src_y - center_y) + b;
            }
            else
            {
                var k = max / (center_y - start_y);
                var b = max;

                reward += k * (src_y - center_y) + b;
            }

            this._reward_text.text = ALittle.Math_Floor(reward);
            this._reward_text.red = 0;
            if (reward < 20) this._reward_text.red = 1;
            this._reward_text.green = 0;
            this._reward_text.blue = 0;
            this._target_x_text.text = start_y;
            this._target_y_text.text = end_y;
        
        }

        return reward;
    }

    private fun CalcPipe() : ALittle.DisplayObject
    {
        for (var i, child in this._pipe_container.childs)
        {
            if (child.x + child.width >= this._bird.x - this._bird.width / 2)
            {
                return child;
            }
        }

        return null;
    }

    public fun LoopGroundFrame(int frame_time)
    {
        var y_delta_time = frame_time / 1000;
        var x_delta_time = frame_time / 20;

        var old_dieing = this._dieing;

        var old_pipe = this.CalcPipe();

        var state:List<double>;
        var action = 0;
        if (this._dqn_model != null)
        {
            state = this.CalcState();

            if (ALittle.Math_RandomInt(1, 1000) < 1000)
            	action = this._dqn_model.ChooseAction(state);
            else
            	action = ALittle.Math_RandomInt(0, 1);
        }

        if (!this._dieing)
        {
            if (action != 0) this._fly_y_rate = -this._fly_power;
        
            var cur_score = cast<int>(this._score_text._user_data) + frame_time;
            this._score_text._user_data = cur_score;
            this._score_text.text = ALittle.Math_Floor(cur_score / 100);
        }

        // 处理地板
        var total_time = (this._ground.width - this._main_scene.width) * 20;
        this._ground_frame_acc += frame_time;
        if (this._ground_frame_acc > total_time)
            this._ground_frame_acc = cast<int>((this._ground_frame_acc % total_time));
        this._ground.x = - this._ground_frame_acc /  20;
        
        // 如果鸟没有到中间，那么就往前飞
        if (this._bird.x < this._bg.width / 2)
        {
            this._bird.x = this._bird.x + x_delta_time;
        }
        else
        {
            // 处理管道
            var create_pipe = false;
            // 如果没有管道，那么就生成一个
            if (this._pipe_container.child_count == 0)
            {
                create_pipe = true;
            }
            // 如果最后一个走的比较远了，那么就生成一个
            else
            {
                var pipe = this._pipe_container.childs[this._pipe_container.child_count];
                var right = pipe.x + pipe.width;
                if (this._bg.width - right > this._next_pipe_gap)
                    create_pipe = true;

                pipe = this._pipe_container.childs[1];
                if (pipe.x + pipe.width < 0)
                    this._pipe_container.RemoveChild(pipe);
            }

            // create_pipe = false;
            if (create_pipe)
            {
                var pipe = g_Control.CreateControl{ALittle.DisplayObject}("pipe");
                this._pipe_container.AddChild(pipe);
            
                var total_height = this._bg.height - this._ground.height;
                var half_height = total_height / 2;

                // 随机上面还是下面
                if (ALittle.Math_RandomInt(1, 100) <= this._next_pipe_up)
                {
                    var min_y = ALittle.Math_Floor(-pipe.height + pipe.height / 4);
                    var max_y = ALittle.Math_Floor(half_height - pipe.height);
                    pipe.y = ALittle.Math_RandomInt(min_y, max_y);
                    // 标记水管是上面
                    pipe._user_data = true;

                    this._next_pipe_up = 30;
                }
                else
                {
                    var min_y = ALittle.Math_Floor(half_height);
                    var max_y = ALittle.Math_Floor(total_height - pipe.height / 4);
                    pipe.y = ALittle.Math_RandomInt(min_y, max_y);
                    // 标记水管是下面
                    pipe._user_data = false;
                
                    this._next_pipe_up = 70;
                }
                pipe.x = this._bg.width;

                this._next_pipe_gap = ALittle.Math_RandomInt(100, 150);
            }

            // 全部向左移动
            for (var index, child in this._pipe_container.childs)
            {
                child.x = child.x - x_delta_time;
            }
        }

        // 计算速度
        this._fly_y_rate += this._fly_a * y_delta_time;
        // 鸟向y轴变化 
        this._bird.y += this._fly_y_rate;

        // 根据速度来设置角度
        if (this._dieing == false)
        {
            if (this._fly_y_rate < 0)
            {
                var rate = 1.0;
                if (-this._fly_y_rate < this._fly_power) rate = -this._fly_y_rate / this._fly_power;
                this._bird.angle = - 45 * rate;
            }
            else
            {
                var rate = 1.0;
                if (this._fly_y_rate < this._fly_power) rate = this._fly_y_rate / this._fly_power;
                this._bird.angle = 45 * rate;
            }
        }

        var new_pipe = this.CalcPipe();

        if (this._dieing)
        {
            // 已经死亡，那么就往下掉
            if (this._bird.y + this._bird.height / 2 >= this._ground.y)
                this.ShowGameOver();
        }
        else
        {
            // 判断死亡
            for (var index, child in this._pipe_container.childs)
            {
                var left = this._bird.x - this._bird.width / 2;
                var right = this._bird.x + this._bird.width / 2;
                var top = this._bird.y - this._bird.height / 2;
                var bottom = this._bird.y + this._bird.height / 2;
                if ((left > child.x && left <= child.x + child.width || right > child.x && right <= child.x + child.width)
                    && (top > child.y && top <= child.y + child.height || bottom > child.y && bottom <= child.y + child.height))
                {
                    this._dieing = true;
                    break;
                }
            }

            if (this._bird.y - this._bird.height / 2 <= 0 || this._bird.y + this._bird.height / 2 >= this._ground.y)
                this._dieing = true;

            if (this._dieing)
            {
                this._bird.angle = 90;
                this._bird.Stop();
                if (this._dqn_model != null)
                {
                    this._dqn_model.Save(this._dqn_model_path);
                    this.ShowGameOver();
                    this.HandleStartClick(null);
                }
            }

            if (this._dqn_model != null && (this._dieing == false || old_dieing == false) && old_pipe == new_pipe)
            {
                var reward = this.CalcReward(this._dieing);
                var next_state = this.CalcState();
                this._dqn_model.SaveTransition(state, action, reward, next_state);
                for (var i = 1; i <= 32; 1)
                    this._dqn_model.Learn();
            }
        }
    }
    
    public fun HandleStartClick(ALittle.UIClickEvent event)
    {
        this._main_menu.visible = false;
        this._main_scene.visible = true;

        this.Restart();
    }
    
    public fun HandleLButtonDown(ALittle.UILButtonDownEvent event)
    {
        this._tip_1.visible = false;
        this._tip_2.visible = false;

        if (this._dqn_model == null && this._dieing == false)
        	this._fly_y_rate = -this._fly_power;
    }
    
    public fun HandleLButtonUp(ALittle.UILButtonUpEvent event)
    {
    }
    
    public fun ShowGameOver()
    {
        this._main_menu.visible = true;
        this._main_scene.visible = false;
    
        this._game_over.visible = true;
        this._game_title.visible = false;
        
        this._bird.Stop();
        this._frame_anti.Stop();
        
        var cur_socre = ALittle.Math_Floor(cast<int>(this._score_text._user_data) / 100);
        if (cast<int>(this._max_score_text._user_data) < cur_socre)
        {
            this._max_score_text._user_data = cur_socre;
            this._max_score_text.text = this._max_score_text._user_data;
            g_GConfig.SetConfig("max_score", cur_socre, null);
        }
    }
    
    public fun Shutdown()
    {
        this._bird.Stop();
        this._frame_anti.Stop();
        this._bird_image_loop.Stop();

        if (this._dqn_model != null)
        {
            this._dqn_model.Save(this._dqn_model_path);
            this._dqn_model = null;
        }
    }
}
    
protected var g_GCenter = new GCenter();